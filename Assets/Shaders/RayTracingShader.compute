// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
float2 _PixelOffset;
float4 _DirectionalLight;
Texture2D _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

static const float PI = 3.14159265359f;
static const float GROUND_PLANE = 0.0f;

// Structs
struct Ray {
    float3 origin;
    float3 direction;
    float3 energy;
};

struct RayHit {
    float3 position;
    float3 normal;
    float distance;
};

// Constructors
Ray CreateRay(float3 origin, float3 direction) {
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.energy = float3(1.0f, 1.0f, 1.0f);
    return ray;
}

RayHit CreateRayHit() {
    RayHit hit;
    hit.position = float3(0.0f, 0.0f, 0.0f);
    hit.normal = float3(0.0f, 0.0f, 0.0f);
    hit.distance = 1.#INF;
    return hit;
}

// Helper functions
Ray CreateCameraRay(float2 uv) {
    // Transform camera origin to world coordinates
    float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;

    // Invert perspective projection of view space position
    float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    // Transform direction from camera to world space
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    // Normalize direction
    direction = normalize(direction);

    return CreateRay(origin, direction);
}

void IntersectGroundPlane(Ray ray, inout RayHit bestHit) {
    float t = (GROUND_PLANE - ray.origin.y) / ray.direction.y;
    if (t > 0 && t < bestHit.distance) {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = float3(0.0f, 1.0f, 0.0f);
    }
}

void IntersectSphere(Ray ray, inout RayHit bestHit, float4 sphere) {
    float3 d = sphere.xyz - ray.origin;
    float p1 = dot(d, ray.direction);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.w * sphere.w; // Where sphere.w is the radius
    if (p2sqr < 0) {
        return; // No solution exists (no intersection)
    }
    float p2 = sqrt(p2sqr);
    
    float t = p1 - p2;
    if (t <= 0) {
        t = p1 + p2;
    }

    if (t > 0 && t < bestHit.distance) {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = normalize(bestHit.position - sphere.xyz);
    }
}

RayHit Trace(Ray ray) {
    RayHit hit = CreateRayHit();
    IntersectGroundPlane(ray, hit);
    IntersectSphere(ray, hit, float4(4, 3, 2, 5));
    IntersectSphere(ray, hit, float4(15, 1, 23, 3));
    IntersectSphere(ray, hit, float4(13, 3, 9, 6));
    IntersectSphere(ray, hit, float4(-7, 5, 8, 8));
    return hit;
}

float3 Shade(inout Ray ray, RayHit hit) {
    if (hit.distance != 1.#INF) {
        // Energy change and color
        float3 specular = float3(1.0f, 0.78f, 0.34f); // Gold reflection
        float3 diffuse = float3(0.8f, 0.8f, 0.8f); // White diffuse

        // Reflect ray
        ray.origin = hit.position + hit.normal * 0.001f;
        ray.direction = reflect(ray.direction, hit.normal);
        ray.energy *= specular;

        // Check for directional light shadow
        bool shade = false;
        Ray shadowRay = CreateRay(hit.position + hit.normal * 0.001f, -1 * _DirectionalLight.xyz);
        RayHit shadowHit = Trace(shadowRay);
        if (shadowHit.distance != 1.#INF) {
            return float3(0.0f, 0.0f, 0.0f);
        }

        // Return diffuse color
        return saturate(dot(hit.normal, _DirectionalLight.xyz) * -1) * _DirectionalLight.w * diffuse;
    } else {
        // No reflection off of sky
        ray.energy = 0.0f;

        // Sample skybox
        float theta = acos(ray.direction.y) / -PI;
        float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
        float3 sample = _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz;

        // Return sampled value
        return sample; 
    }
}

// Main rendering code
[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID) {
    // Get dimensions of render texture
    uint width, height;
    Result.GetDimensions(width, height);

    // Transform current pixel to the range [-1, 1]
    float2 uv = float2((id.xy + _PixelOffset) / float2(width, height) * 2.0f - 1.0f);

    // Create a ray for this pixel
    float3 result = float3(0.0f, 0.0f, 0.0f);
    Ray ray = CreateCameraRay(uv);
    for (int i = 0; i < 8; i++) {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);

        // Check ray energy
        if (!any(ray.energy)) {
            break;
        }
    }

    // Write colors
    Result[id.xy] = float4(result, 1.0f);
}
